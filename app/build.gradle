apply plugin: 'com.android.application'
apply from: 'quality.gradle'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def date = new Date();
def buildDate = date.format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))
def externalPrivateVersion = new File(project.rootDir.path + "/app/external_version_code.txt").text.trim().toInteger()
def externalPublicVersion = new File(project.rootDir.path + "/app/external_version_name.txt").text.trim()
def internalPrivateVersion = new File(project.rootDir.path + "/app/internal_version_code.txt").text.trim().toInteger()
def internalPublicVersion = new File(project.rootDir.path + "/app/internal_version_name.txt").text.trim()
def isCircle = "true".equals(System.getenv("CIRCLECI"))

ext.signing = [
    storeFilePath : "path/to/keystore",
    storePassword : "keystore password",
    keyAlias      : "key alias",
    keyPassword   : "key password"
]

if (file('signing.gradle').exists()) {
    apply from: 'signing.gradle'
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "com.kickstarter"
        minSdkVersion 17
        targetSdkVersion 25
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_DATE", "\"${buildDate}\""

        // Enable Multidex to support over 65K methods
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file(project.signing.storeFilePath)
            storePassword project.signing.storePassword
            keyAlias project.signing.keyAlias
            keyPassword project.signing.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            if (isCircle) {
                // The release build generated on CircleCI doesn't need to be signed with our real
                // keystore - we just need a release build to verify that it compiles. Using the
                // debug keystore means we don't have to expose our keystore.
                signingConfig signingConfigs.debug
            } else {
                signingConfig signingConfigs.release
            }
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "AUDIENCE", "API"

    productFlavors {
        pre21 {
            dimension "API"
            // Don't override the minSdkVersion, just inherit from defaultConfig
        }
        min21 {
            // minSdkVersion 21 speeds up local development by generating multidex output much faster.
            // It should not be used for release builds. See:
            // https://developer.android.com/tools/building/multidex.html#dev-build
            dimension "API"
            minSdkVersion 21
        }
        internal {
            dimension "AUDIENCE"
            applicationId "com.kickstarter.kickstarter.internal"
            versionCode internalPrivateVersion
            versionName internalPublicVersion
        }
        external {
            dimension "AUDIENCE"
            applicationId "com.kickstarter.kickstarter"
            versionCode externalPrivateVersion
            versionName externalPublicVersion
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Fixes build error hitting GC overhead limit
    dexOptions {
        javaMaxHeapSize "2560M"
    }

    lintOptions {
        quiet true
        warningsAsErrors true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
    }

    sourceSets {
        androidTest {
            java.srcDir file('src/androidTest')
        }
        main.java.srcDirs += 'src/main/kotlin'
    }
}

repositories {
    jcenter()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:cardview-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile 'com.android.support:multidex:1.0.1'
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:support-annotations:$supportLibVersion"
    compile 'com.facebook.android:facebook-android-sdk:4.7.0'
    compile "com.github.frankiesardo:auto-parcel:$autoparcelVersion"
    annotationProcessor "com.github.frankiesardo:auto-parcel-processor:$autoparcelVersion"
    compile "com.google.android.gms:play-services-gcm:$playservicesVersion"
    compile "com.google.android.gms:play-services-wallet:$playservicesVersion"
    compile 'com.google.android.exoplayer:exoplayer:r1.5.5'
    compile "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton:process-phoenix:1.0.2'
    compile 'com.jakewharton.timber:timber:3.0.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.stripe:stripe-android:1.0.3'
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    compile "com.squareup.okhttp3:okhttp:$okhttp3Version"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttp3Version"
    compile "com.squareup.okhttp3:okhttp-urlconnection:$okhttp3Version"
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile "com.squareup.retrofit2:retrofit:$retrofit2Version"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofit2Version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit2Version"
    compile "com.trello:rxlifecycle:$rxlifecycleVersion"
    compile "com.trello:rxlifecycle-components:$rxlifecycleVersion"
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'
    compile "com.jakewharton.rxbinding:rxbinding:$rxbindingVersion"
    compile "com.jakewharton.rxbinding:rxbinding-recyclerview-v7:$rxbindingVersion"
    compile "com.jakewharton.rxbinding:rxbinding-support-v4:$rxbindingVersion"
    compile 'net.danlew:android.joda:2.7.2'
    compile 'net.hockeyapp.android:HockeySDK:3.6.1'
    compile 'org.jsoup:jsoup:1.8.2'
    compile "com.mixpanel.android:mixpanel-android:4.6.4"

    // Testing
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "org.robolectric:robolectric:$robolectricVersion"
    testCompile "org.robolectric:shadows-multidex:$robolectricVersion"
    testCompile "org.robolectric:shadows-play-services:$robolectricVersion"
    testCompile "org.robolectric:shadows-support-v4:$robolectricVersion"
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile "com.android.support:support-annotations:$supportLibVersion"
}

buildscript {
    ext.kotlin_version = '1.0.6'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.4.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

// Copy google-services.json from variant directory to root of app
gradle.taskGraph.beforeTask { Task task ->
    if (task.name ==~ /process.*GoogleServices/) {
        android.applicationVariants.all { variant ->
            if (task.name ==~ /(?i)process${variant.name}GoogleServices/){
                copy {
                    from "src/${variant.name}"
                    into '.'
                    include 'google-services.json'
                }
            }
        }
    }
}

apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
